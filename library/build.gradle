buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 'Google Inc.:Google APIs:23'
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'org.threeten:threetenbp:1.3.1'
}

group = 'solar.blaz'
version = '1.1-SNAPSHOT'

bintray {
    user = bintrayUser
    key = bintrayKey

    publish = true

    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'maven'
        name = 'week-date-picker'
        desc = 'Lightweight week date picker implmentation for Android.'
        websiteUrl = "https://github.com/blazsolar/WeekDatePicker"
        issueTrackerUrl = 'https://github.com/blazsolar/WeekDatePicker/issues'
        vcsUrl = "https://github.com/blazsolar/WeekDatePicker"
        licenses = ['MIT']
        labels = ['aar', 'android', 'date']
        publicDownloadNumbers = true

        githubRepo = 'blazsolar/WeekDatePicker'
        githubReleaseNotesFile = 'README.md'
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'Lightweight week date picker implmentation for Android.'
                url "https://github.com/blazsolar/WeekDatePicker"
                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://rem.mit-license.org'
                    }
                }
                developers {
                    developer {
                        id "blazsolar"
                        name "Blaz Solar"
                    }
                }
                scm {
                    url "https://github.com/blazsolar/WeekDatePicker"
                    connection "scm:git@github.com:blazsolar/WeekDatePicker.git"
                    developerConnection "scm:git@github.com:blazsolar/WeekDatePicker.git"
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
